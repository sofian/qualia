import os

# From http://code.activestate.com/recipes/502263/
# By Paul Rubin
def unique(seq, keepstr=True):
  t = type(seq)
  if t in (str, unicode):
    t = (list, ''.join)[bool(keepstr)]
  seen = []
  return t(c for c in seq if not (c in seen or seen.append(c)))

# General arguments

# Get mode.
platform  = ARGUMENTS.get("platform", "computer")
mode      = ARGUMENTS.get("mode", "release")
precision = ARGUMENTS.get("precision", "float")

TARGET = None

INCLUDE_PATH = ""
MCU = 'atmega168'
F_CPU = 16000000
EXTRA_LIBS = ""
LIBS = ""
LIBPATH = ""
SOURCEPATH = ""
AVR_GCC_DIR = ""
QUALIA_DIR = ""

if (platform == 'avr' or platform == 'arduino'):
  AVR_GCC_DIR = "/Applications/Arduino.app/Contents/Resources/Java/hardware/tools/avr/bin/"

# Import settings
CONFIG = ['INCLUDE_PATH', 'MCU', 'F_CPU', 'EXTRA_LIBS', 'AVR_GCC_DIR', 'TARGET', 'LIBS', 'LIBPATH', 'SOURCEPATH', 'QUALIA_DIR', 'platform', 'mode']
conf = SConscript(dirs='.', exports=CONFIG)
for i in range(len(CONFIG)):
  vars()[CONFIG[i]] = conf[i]

if TARGET == None:
  TARGET = COMMAND_LINE_TARGETS[0]

INCLUDE_PATH = ARGUMENTS.get('INCLUDE_PATH', INCLUDE_PATH).split(":")
INCLUDE_PATH = unique(INCLUDE_PATH + [os.getcwd()])

# AVR arguments
#MCU = getBoardConf(r'^%s\.build\.mcu=(.*)'%ARDUINO_BOARD)
MCU = ARGUMENTS.get('MCU', MCU)
#F_CPU = getBoardConf(r'^%s\.build\.f_cpu=(.*)'%ARDUINO_BOARD)
F_CPU = ARGUMENTS.get('F_CPU', F_CPU)

QUALIA_DIR = ARGUMENTS.get('QUALIA_DIR', QUALIA_DIR)

# Arduino arguments
EXTRA_LIBS = ARGUMENTS.get('EXTRA_LIBS', EXTRA_LIBS).split(':')

# Shared library arguments.
LIBS = ARGUMENTS.get('LIBS', LIBS).split(',')
LIBPATH = ARGUMENTS.get('LIBPATH', LIBPATH).split(':')

BUILD_DIR = "build/" + platform + "/"

LIBS += ["qualia", "m"]
LIBPATH += [ QUALIA_DIR + "/" + BUILD_DIR + "/qualia"]
INCLUDE_PATH += [ QUALIA_DIR + "/src"]

# There should be a file with the same name as the folder and with the extension .pde
#TARGET = os.path.basename(os.path.realpath(os.curdir))
#assert(os.path.exists(TARGET+'.pde'))

AVR_GCC_DIR = ARGUMENTS.get('AVR_GCC_DIR', AVR_GCC_DIR)
if AVR_GCC_DIR:
  AVR_GCC_DIR += '/'

AVR_BIN_PREFIX = AVR_GCC_DIR + 'avr-'

SOURCEPATH = ARGUMENTS.get('SOURCEPATH', SOURCEPATH).split(':');

sources = []
for dir in SOURCEPATH:
	sources += Glob(dir + "/*.cpp")
	sources += Glob(dir + "/*.cxx")
	sources += Glob(dir + "/*.c")

# Remove empty items
LIBS = filter(None, LIBS)
LIBPATH = filter(None, LIBPATH)

# Create environment and set default configurations ###################################
if (platform == 'avr' or platform == 'arduino'):
  cFlags = ['-ffunction-sections', '-fdata-sections', '-fno-exceptions',
            '-funsigned-char', '-funsigned-bitfields', '-fpack-struct', '-fshort-enums',
            '-Os', '-mmcu=%s'%MCU]
  cppDefs = {'F_CPU':F_CPU};
  if (precision == 'double'):
    cppDefs['USE_DOUBLE'] = 1
  env = Environment(CC = AVR_BIN_PREFIX+'gcc',
                    CXX = AVR_BIN_PREFIX+'g++',
                    AS = AVR_BIN_PREFIX + 'gcc',
                    CPPPATH = INCLUDE_PATH,
                    LIBPATH = LIBPATH, # path to qualia static lib
                    LIBS = LIBS, 
                    CPPDEFINES = cppDefs,
                    CFLAGS = cFlags + ['-std=gnu99'], 
                    CCFLAGS = cFlags, 
                    ASFLAGS = ['-assembler-with-cpp','-mmcu=%s' % MCU],
                    LINKFLAGS = ['-mmcu=%s' % MCU ])

  libPathFlags = ' '.join([ "-L" + x for x in LIBPATH ])
  libFlags    = ' '.join([ "-l" + x for x in LIBS ])
  env.Append(BUILDERS = {'Elf':Builder(action=AVR_BIN_PREFIX+'gcc -mmcu=%s ' % MCU +
                         '-Os -Wl,--gc-sections,--relax -o $TARGET $SOURCES ' + libPathFlags + ' ' + libFlags)})
  env.Append(BUILDERS = {'Hex':Builder(action=AVR_BIN_PREFIX+'objcopy '+
                         '-O ihex -R .eeprom $SOURCES $TARGET')})
  
  env.VariantDir(BUILD_DIR, ".", duplicate=0)
  
  sources += Glob(BUILD_DIR + "*.cpp")
  sources += Glob(BUILD_DIR + "*.cxx")
  sources += Glob(BUILD_DIR + "*.c")
  
  objs = env.Object(sources)
  env.Elf(BUILD_DIR + TARGET + '.elf', objs)
#  env.Program(target = BUILD_DIR + TARGET + '.elf', source = sources, 
#  					  CPPFLAGS = ['-mmcu=%s' % MCU, '-Os'],
#  						LINKFLAGS = "-Wl,--gc-sections,--relax", )
  env.Hex(BUILD_DIR + TARGET + '.hex', BUILD_DIR + TARGET + '.elf')
  
  #MAX_SIZE = getBoardConf(r'^%s\.upload.maximum_size=(.*)'%ARDUINO_BOARD)
  #print ("maximum size for hex file: %s bytes"%MAX_SIZE)
  env.Command(None, BUILD_DIR + TARGET+'.hex', AVR_BIN_PREFIX+'size --target=ihex $SOURCE')

else:
  env = Environment()
  env.Append(CPPPATH=["/usr/local/include", "/usr/include", os.getcwd()])
  if (mode == 'debug'):
    env.Append(CCFLAGS=['-Wall', '-g', '-DDEBUG=1'])
  else:
    env.Append(CCFLAGS=['-O2'])

  if (precision == 'double'):
    env.Append(CCFLAGS=['-DUSE_DOUBLE'])

  env.VariantDir(BUILD_DIR, ".", duplicate=0)
  sources += Glob(BUILD_DIR + "*.cpp")
  sources += Glob(BUILD_DIR + "*.c")
  
  env.Program(BUILD_DIR + TARGET, sources, LIBS = LIBS, CPPPATH = INCLUDE_PATH, LIBPATH = LIBPATH)


  #objects = env.StaticObject(source = sources)

  # Peut etre une erreur: on devrait construire des OBJETS (?)
  #lib = env.Library(target = target, source = sources)

#execfile("../../tools/scons/SConstruct")
